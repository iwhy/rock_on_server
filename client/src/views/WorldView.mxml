<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:views="views.*" width="1000" height="1000" clipContent="false" creationComplete="onCreationComplete()" verticalScrollPolicy="off" horizontalScrollPolicy="off">	
	<mx:Script>
		<![CDATA[
			import controllers.CreatureManager;
			import controllers.DwellingManager;
			import controllers.LayerableManager;
			import controllers.LevelManager;
			import controllers.StoreManager;
			import controllers.StructureManager;
			import controllers.ThingerManager;
			import controllers.UserManager;
			
			import flash.utils.getDefinitionByName;
			import flash.utils.getQualifiedClassName;
			import flash.utils.getTimer;
			
			import flashx.textLayout.container.ScrollPolicy;
			
			import game.GameClock;
			import game.GameMagicManager;
			
			import helpers.CreatureGenerator;
			
			import models.Creature;
			import models.EssentialModelReference;
			import models.OwnedDwelling;
			import models.OwnedLayerable;
			import models.OwnedStructure;
			import models.OwnedThinger;
			import models.Store;
			import models.StoreOwnedThinger;
			import models.Structure;
			import models.Thinger;
			import models.User;
			
			import mx.collections.ArrayCollection;
			import mx.containers.Canvas;
			import mx.controls.Alert;
			import mx.controls.Button;
			import mx.controls.Text;
			import mx.core.Application;
			import mx.core.DragSource;
			import mx.core.UIComponent;
			import mx.events.DragEvent;
			import mx.events.DynamicEvent;
			import mx.events.FlexEvent;
			import mx.managers.CursorManager;
			import mx.managers.DragManager;
			import mx.states.AddChild;
			
			import rock_on.BandMember;
			import rock_on.BandMemberManager;
			import rock_on.Booth;
			import rock_on.BoothManager;
			import rock_on.ConcertStage;
			import rock_on.CustomerPerson;
			import rock_on.CustomerPersonManager;
			import rock_on.GroupieManager;
			import rock_on.ListeningStation;
			import rock_on.ListeningStationManager;
			import rock_on.PasserbyManager;
			import rock_on.Person;
			import rock_on.Promoter;
			import rock_on.PromoterManager;
			import rock_on.StageManager;
			
			import stores.StoreEvent;
			
			import user.UserEvent;
			
			import world.ActiveAsset;
			import world.AssetGenius;
			import world.AssetStack;
			import world.BitmapBlotter;
			import world.Point3D;
			import world.World;
			import world.WorldEvent;	
			
			public var myWorld:World;
			[Bindable] public var myUser:User;
			
			public static const PERSON_SPEED:Number = 0.07;
			public static const WINDOW_WIDTH:int = 1000;
			public static const WINDOW_HEIGHT:int = 1000;
			
			public static var worldWidth:int = 1200;
			public static var worldHeight:int = 1200;
			public static var tileSize:int = 40;
			
			public var stageView:StageView;
			[Bindable] public var bitmapLayer:Bitmap;
			public var backgroundRect:Rectangle;
			public var backgroundPoint:Point;
			[Bindable] public var bitmapBlotter:BitmapBlotter;
			public var backgroundCanvas:Canvas;
			public var bottomBar:BottomBar;
			public var assetGenius:AssetGenius;
			public var inventoryCanvas:Canvas;
			public var inventoryIndex:int = 0;
			public var uiLayer:UILayer;
			public var editMode:EditMode;
			public var editModeActivated:Boolean = false;
			public var booths:ArrayCollection;
			public var stages:ArrayCollection;
			[Bindable] public var customerPersonManager:CustomerPersonManager;
			[Bindable] public var bandManager:BandManager;
			[Bindable] public var concertStage:ConcertStage;
			public var listeningStationManager:ListeningStationManager;
			public var groupieManager:GroupieManager;
			[Bindable] public var venueManager:VenueManager;
			[Bindable] public var boothManager:BoothManager;
			public var stageManager:StageManager;
			public var creatureGenerator:CreatureGenerator;
			public var creditsBox:Text;
			public var myStage:World;
			public var creaturesAdded:Boolean;
			public var wbi:WorldBitmapInterface;
			
			[Bindable] public var gameMagicManager:GameMagicManager;			
			[Bindable] public var layerableManager:LayerableManager;
			[Bindable] public var structureManager:StructureManager;
			[Bindable] public var storeManager:StoreManager;
			[Bindable] public var creatureManager:CreatureManager;
			[Bindable] public var thingerManager:ThingerManager;
			[Bindable] public var userManager:UserManager;
			[Bindable] public var dwellingManager:DwellingManager;
			[Bindable] public var levelManager:LevelManager;
			
			[Bindable] public var myMemory:Number;
			[Bindable] public var fps:Number;
			[Bindable] public var aux:Number;	
			public var lastTime:Number;	
			
			public var isDragging:Boolean;
			public var cursorUIC:ContainerUIC;
			public var mouseIncrementX:Number;
			public var mouseIncrementY:Number;
			
			private function onCreationComplete():void
			{
				addEventListener(Event.ENTER_FRAME, onEnterFrame);
				addEventListener(MouseEvent.MOUSE_DOWN, onMouseDown);	
				addEventListener(MouseEvent.CLICK, onViewClicked);
			}
			
			private function onViewClicked(evt:MouseEvent):void
			{
				wbi.onWorldViewClicked(evt);
			}
			
			public function initializer():void
			{
				addWorld();
				addUILayer();
				
				customerPersonManager = new CustomerPersonManager();
				wbi.customerPersonManager = customerPersonManager;
				customerPersonManager.myWorld = myWorld;				
				var evt:DynamicEvent = new DynamicEvent("worldAdded", true, true);
				dispatchEvent(evt);
			}
			
			public function initializeStructures():void
			{
				initializer();
				boothManager = new BoothManager(structureManager, myWorld);
				creatureGenerator = new CreatureGenerator(layerableManager);
				generateManagers();				
				initializeVenue();	
				addStructuresToVenue();
			}
			
			public function initializeCreatures():void
			{
				checkForCreaturesAdded();				
			}
			
			private function checkForCreaturesAdded():void
			{
				if (creaturesAdded)
				{
					addCreaturesToVenue();
				}
				else
				{
					creaturesAdded = true;	
				}				
			}
			
			private function onAssetsDrawn(evt:WorldEvent):void
			{
				checkForCreaturesAdded();
			}
			
			public function addStructuresToVenue():void
			{
				showBooths();
				showListeningStations();				
			}
			
			public function addCreaturesToVenue():void
			{
				showStaticListeners();
				showPassersby();
				showVenue();		
				showGroupies();	
			}
			
			private function onMouseDown(evt:MouseEvent):void
			{
				wbi.onMouseDown(evt);
			}
			
			public function addBackgroundLayer():void
			{
				var relWidth:Number = myWorld.wg.getRect(myWorld).width;
				var relHeight:Number = myWorld.wg.getRect(myWorld).height;
				bitmapBlotter = new BitmapBlotter(relWidth, relHeight, true, 0x00000000);
				wbi.bitmapBlotter = bitmapBlotter;
				bitmapBlotter.myWorld = myWorld;
				bitmapBlotter.incrementX = (width - relWidth)/2;
				bitmapBlotter.incrementY = (height - relHeight)/2;
				bitmapBlotter.relWidth = relWidth;
				bitmapBlotter.relHeight = relHeight;
				myWorld.bitmapBlotter = bitmapBlotter;
				bitmapLayer = new Bitmap(bitmapBlotter);
				bitmapLayer.opaqueBackground = null;
				bitmapLayer.width = relWidth;
				bitmapLayer.height = relHeight;
				bitmapLayer.bitmapData = bitmapBlotter;
				backgroundCanvas = new Canvas();
				wbi.backgroundCanvas = backgroundCanvas;
				backgroundCanvas.width = relWidth;
				backgroundCanvas.height = relHeight;
				backgroundCanvas.clipContent = false;
				backgroundCanvas.x = (width - relWidth)/2;
				backgroundCanvas.y = (height - relHeight)/2;
				backgroundCanvas.setStyle("backgroundAlpha", 0);
				bitmapBlotter.backgroundCanvas = backgroundCanvas;
				
				addChild(backgroundCanvas);
				backgroundCanvas.cachePolicy = "on";
				backgroundCanvas.nestLevel = 2;				
			}
			
			public function addStageLayer():void
			{
				createStage();
				myStage = new World(concertStage.structure.width*tileSize, concertStage.structure.height*tileSize, tileSize, concertStage.structure.height);
				myStage.nestLevel = 1;
				addChild(myStage);
				stageManager.addStageToWorld(stageManager.stageAsset, concertStage.worldCoords, myStage);
			}
			
			public function addWorld():void
			{
				myWorld = new World(worldWidth, worldHeight, tileSize, concertStage.structure.height);	
				myWorld.nestLevel = 3;
				addBackgroundLayer();
				addChild(myWorld);	
				myWorld.assetRenderer.addEventListener(WorldEvent.ASSETS_DRAWN, onAssetsDrawn);
			}
			
			public function generateManagers():void
			{
				listeningStationManager = new ListeningStationManager(structureManager, layerableManager, myWorld);
				groupieManager = new GroupieManager(customerPersonManager, boothManager, concertStage, creatureManager, myWorld);
				stageView.initializeBandManager();
				venueManager = new VenueManager(creatureGenerator, customerPersonManager, dwellingManager, levelManager, bandManager, boothManager, stageManager, myWorld);
			}

			private function initializeVenue():void
			{
				venueManager.getVenue();
				
				getNumberOfBitmaps();
				
				boothManager.venue = venueManager.venue;
				listeningStationManager.venue = venueManager.venue;
				customerPersonManager.venue = venueManager.venue;
				bandManager.venue = venueManager.venue;	
				groupieManager.venue = venueManager.venue;
			}
			
			public function showStaticListeners():void
			{
				listeningStationManager.addStaticStationListeners();
			}
			
			private function getNumberOfBitmaps():void
			{
				var totalStructures:int = 0;
				for each (var os:OwnedStructure in structureManager.owned_structures)
				{
					if (os.structure.structure_type == "Booth")
					{
						totalStructures++;
					}
				}
				bitmapBlotter.expectedAssetCount = venueManager.numStaticCustomers + totalStructures;				
			}
			
			private function showVenue():void
			{
				venueManager.addStaticCustomersToVenue();
				venueManager.addSuperCustomersToVenue(stageView.myStage);
				venueManager.addMovingCustomersToVenue();
			}
			
			private function showListeningStations():void
			{
				listeningStationManager.uiLayer = uiLayer;				
				listeningStationManager.setInMotion();	
				
				listeningStationManager.boothManager = boothManager;
				listeningStationManager.stageManager = stageManager;				
				listeningStationManager.customerPersonManager = customerPersonManager;			
			}
			
			private function showPassersby():void
			{
				listeningStationManager.showPassersby();
			}
			
			private function onEnterFrame(evt:Event):void
			{
				var time:Number = getTimer();
				var deltaTime:Number = time - lastTime;
				var lockedDelta:Number = Math.min(100, deltaTime);
				fps = 1000/deltaTime;
				myMemory = System.totalMemory;
				lastTime = time;
				
				if (wbi)
				{
					wbi.handleEnterFrameEvents();				
				}
				
				checkCustomerPersonManager(lockedDelta);
			}
			
			public function addUILayer():void
			{
				uiLayer = new UILayer();
				uiLayer.width = this.width;
				uiLayer.height = this.height;
				addChild(uiLayer);				
			}			
			
			public function checkCustomerPersonManager(lockedDelta:Number):void
			{
				if (customerPersonManager)
				{
					customerPersonManager.update(lockedDelta);				
				}				
			}
			
			public function onUserLoaded():void
			{
				
			}
			
			private function onStructurePlaced(evt:DynamicEvent):void
			{
				thingerManager.saveStructurePlacement(evt.asset, evt.currentPoint);
			}
			
			private function onWorldClicked(evt:MouseEvent):void
			{
				if (evt.target is BandMember)
				{
					showCustomizer((evt.target as ActiveAsset).thinger as Creature);
				}
				else if (evt.target is CustomerPerson)
				{
					if (((evt.target as ActiveAsset).thinger as Creature).type == "Groupie")
					{
						Alert.show("I'm a Groupie!");
					}
					else 
					{
						Alert.show("Not a Groupie!");
					}
				}
				else
				{
					if (editMode.locked == false)
					{
						editMode.onWorldClicked(evt);					
					}
				}
			}
			
			public function showBooths():void
			{
				boothManager.uiLayer = uiLayer;
				boothManager.setInMotion();
			}
			
			public function createStage():void
			{
				stageManager = new StageManager(structureManager, myStage);
				stageManager.createStage();
				concertStage = stageManager.concertStage;
			}
			
			public function boothPlaced():void
			{
				boothManager.reInitializeBooths(true);
			}
			
			public function showGroupies():void
			{	
				groupieManager.setInMotion();
			}
			
			public function showBandMembers():void
			{
				bandManager.setInMotion();
			}
			
			public function showCustomizer(creature:Creature):void
			{
				parentApplication.onCreatureToCustomize(creature);
			}
			
			public function updateRenderedStructures(os:OwnedStructure, method:String):void
			{
				if (isValidWorldStructure(os))
				{
					var asset:ActiveAsset = myWorld.findAssetByThinger(os);
					if (asset && method == "sell")
					{
						myWorld.assetRenderer.removeAsset(asset);
						reInitializeStructures(os);					
					}
					if (asset && method == "save_placement")
					{
						myWorld.updateAssetCoords(asset, new Point3D(os.x, os.y, os.z), true);
						reInitializeStructures(os);					
					}
					if (method == "create_new")
					{
						addNewOwnedStructure(os);
					}
				}
			}
			
			public function isValidWorldStructure(os:OwnedStructure):Boolean
			{
				if (os.structure.structure_type == "Booth" || os.structure.structure_type == "ListeningStation")
				{
					return true;
				}
				return false;
			}
			
			public function addNewOwnedStructure(os:OwnedStructure):void
			{
				var mc:MovieClip = EssentialModelReference.getMovieClipCopy(os.structure.mc);
				var asset:ActiveAsset = new ActiveAsset(mc);
				var addTo:Point3D = new Point3D(os.x, os.y, os.z);					
				myWorld.addStaticAsset(asset, addTo);
				reInitializeStructures(os);					
			}
			
			public function reInitializeStructures(os:OwnedStructure):void
			{				
				if (os.structure.structure_type == "Booth")
				{
					boothManager.reInitializeBooths(true);
					customerPersonManager.redrawAllCustomers();
					this.bitmapBlotter.renderReplacedBitmaps();
				}
				else if (os.structure.structure_type == "ListeningStation")
				{
					listeningStationManager.reInitializeListeningStations();				
				}
			}
			
		]]>
	</mx:Script>	
</mx:Canvas>
