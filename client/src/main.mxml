<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" xmlns:world="world.*" xmlns:views="views.*" preinitialize="onPreInitialize()" applicationComplete="onApplicationComplete()">
	<mx:states>
		<mx:State name="topBar">
			<mx:AddChild position="after">
				<views:TopBar id="topBarView" myUser="{gdi.user}" editView="{editView}" worldView="{worldView}" userController="{gdi.userController}" songController="{gdi.songController}" storeController="{gdi.storeController}" levelController="{gdi.levelController}"/>
			</mx:AddChild>
			<mx:AddChild position="after">
				<views:BottomBar id="bottomBarView" myUser="{gdi.user}" editView="{editView}" worldView="{worldView}" userController="{gdi.userController}" friendManager="{friendManager}" levelController="{gdi.levelController}" storeController="{gdi.storeController}" thingerController="{gdi.thingerController}"/>
			</mx:AddChild>
			<mx:AddChild position="after">
				<views:UILayer id="uiLayer"/>
			</mx:AddChild>
		</mx:State>
		<mx:State name="stageViewer" basedOn="worldView">
			<mx:AddChild position="before" relativeTo="worldView">
				<views:StageView id="stageView" wbi="{wbi}" bottomBar="{bottomBarView}" venueManager="{worldView.venueManager}" customerPersonManager="{worldView.customerPersonManager}" creatureController="{gdi.creatureController}" structureController="{gdi.structureController}" boothBoss="{worldView.boothBoss}"/>
			</mx:AddChild>			
		</mx:State>
		<mx:State name="worldView" basedOn="topBar">
			<mx:AddChild position="after">
				<views:WorldView id="worldView" wbi="{wbi}" bottomBar="{bottomBarView}" songController="{gdi.songController}" stageView="{stageView}" myUser="{gdi.user}" gameMagicManager="{gameMagicManager}" userController="{gdi.userController}" thingerController="{gdi.thingerController}" storeController="{gdi.storeController}" creatureController="{gdi.creatureController}" structureController="{gdi.structureController}" layerableController="{gdi.layerableController}" dwellingController="{gdi.dwellingController}" levelController="{gdi.levelController}" stageManager="{stageView.stageManager}" concertStage="{stageView.concertStage}" bandBoss="{stageView.bandBoss}"/>
			</mx:AddChild>
		</mx:State>
		<mx:State name="friendView" basedOn="topBar">
			<mx:AddChild>
				<views:FriendView id="friendView"/>
			</mx:AddChild>
		</mx:State>
		<mx:State name="editView" basedOn="topBar">
			<mx:AddChild>
				<views:EditView id="editView" myUser="{gdi.user}" stageView="{stageView}" userController="{gdi.userController}" thingerController="{gdi.thingerController}" storeController="{gdi.storeController}" creatureController="{gdi.creatureController}" structureController="{gdi.structureController}" layerableController="{gdi.layerableController}" boothBoss="{worldView.boothBoss}" venueManager="{worldView.venueManager}" dwellingController="{gdi.dwellingController}"/>
			</mx:AddChild>
		</mx:State>
		<mx:State name="twoDimensional">
			<mx:AddChild>
				<views:TwoDimensionalView id="twoDimensionalView" />
			</mx:AddChild>
		</mx:State>
	</mx:states>
	<mx:Script>
		<![CDATA[
			import com.facebook.data.users.FacebookUser;
			import com.facebook.events.FacebookEvent;			
			import controllers.CreatureController;
			import controllers.FriendController;			
			import facebook.FacebookFunTime;			
			import flash.utils.Proxy;			
			import game.GameDataInterface;
			import game.GameMagicManager;
			import game.ImposterOwnedStructure;
			import models.Creature;
			import models.OwnedDwelling;
			import models.OwnedLayerable;
			import models.OwnedStructure;
			import models.User;
			import mx.events.CollectionEvent;
			import mx.events.DynamicEvent;
			import mx.events.FlexEvent;
			import mx.events.StateChangeEvent;
			import rock_on.ContentIndex;
			import server.ServerController;
			import server.ServerDataEvent;
			import stores.StoreEvent;
			import user.UserEvent;
			import views.CreatureCustomizer;
			import views.WorldBitmapInterface;
			import world.ActiveAsset;
			import world.AssetStack;
						
			[Bindable] public var gdi:GameDataInterface;
			[Bindable] public var wbi:WorldBitmapInterface;
			[Bindable] public var loadedModels:Dictionary;
			[Bindable] public var xGridCoord:Number;
			[Bindable] public var yGridCoord:Number;
			[Bindable] public var gameMagicManager:GameMagicManager;
			[Bindable] public var friendManager:FriendController;
			
			public var facebookInterface:FacebookFunTime;
			public var gameContent:Dictionary;
			public var userLoaded:Boolean;
			public var friendsLoaded:Boolean;
			public var facebookDataLoaded:Boolean;
			
			private function onPreInitialize():void
			{
				Security.allowDomain("*"); 					
				
				gdi = new GameDataInterface();
				
				gdi.addEventListener(ServerDataEvent.USER_LOADED, onUserObjectLoaded);
				gdi.addEventListener(ServerDataEvent.GAME_CONTENT_LOADED, onGameContentLoaded);
				gdi.addEventListener(ServerDataEvent.USER_CONTENT_LOADED, onUserContentLoaded);	
				gdi.addEventListener(ServerDataEvent.UPDATE_COMPLETE, onUpdateComplete);
				
				initializeFriendManager();
				
				gameMagicManager = new GameMagicManager(gdi.levelController, gdi.userController);

//				Call when in non-Facebook test mode
				
//				onFacebookUserLoaded(202357);
			}
			private function initializeFriendManager():void
			{
				friendManager = new FriendController(gdi);	
				friendManager.addEventListener("friendDataLoaded", onFriendDataLoaded);				
			}	
			private function onApplicationComplete():void
			{
				initializeFacebookInterface();				
				currentState = "stageViewer";
				gdi.storeController.addEventListener(StoreEvent.THINGER_PURCHASED, onThingerPurchased);
				
				wbi = new WorldBitmapInterface(worldView, stageView, editView, bottomBarView);
			}
			private function initializeFacebookInterface():void
			{
				facebookInterface = new FacebookFunTime();
				addEventListener("facebookDataLoaded", onFacebookDataLoaded);				
			}
			public function onFacebookUserLoaded(uid:int, evt:FacebookEvent):void
			{
				gdi.getStaticGameContent();
				gdi.getUserContent(uid);
			}
			
// 			Only for non-Facebook version
			
//			public function onFacebookUserLoaded(uid:int):void
//			{
//				gdi.getStaticGameContent();
//				gdi.getUserContent(uid);
//			}
			
//			Only for non-Facebook version			
			
			public function onFacebookDataLoaded(evt:DynamicEvent):void
			{
				facebookDataLoaded = true;
				friendManager.setFacebookData(evt.facebookFriends, evt.facebookUser);
				getBasicFriendInfo();
			}
			public function onFriendDataLoaded(evt:DynamicEvent):void
			{
				friendsLoaded = true;
				bottomBarView.friendDataLoaded(evt);
			}
			public function getBasicFriendInfo():void
			{
				if (userLoaded && facebookDataLoaded)
				{
					friendManager.getFriendGDIs();								
				}
			}
			public function getFriendData(uid:int):void
			{
				friendManager.getFriendData(uid);
			}
			public function setGameContent():void
			{
				gameContent = new Dictionary();
				gameContent["structures"] = gdi.structureController.structures;
				gameContent["layerables"] = gdi.layerableController.layerables;
				gameContent["stores"] = gdi.storeController.stores;
				gameContent["essentialModelReference"] = gdi.essentialModelController.essentialModelReference;
			}
			public function attemptToInitializeVenueForUser():void
			{
				if (!userLoaded)
				{
					userLoaded = true;
					worldView.initializeVenue();
					stageView.initializeStage();
					worldView.initializeWorld();
					worldView.initializeStructures();
					stageView.initializeStructures();
					topBarView.onVenueLoaded();
					getBasicFriendInfo();			
				}
			}
			public function onSongsLoaded():void
			{
				topBarView.onSongsLoaded();
			}
			public function attemptToPopulateVenueForUser():void
			{
				worldView.initializeCreatures();
			}
			public function friendGDILoaded(friendGDI:GameDataInterface):void
			{
				friendManager.friendGDILoaded(friendGDI);
			}
			public function attemptToShowFriendVenue(friendGDI:GameDataInterface):void
			{
				currentState = "friendView";
				friendView.myUser = friendGDI.user;
				friendView.userController = friendGDI.userController;
				friendView.structureController = friendGDI.structureController;
				friendView.layerableController = friendGDI.layerableController;
				friendView.creatureController = friendGDI.creatureController;
				friendView.levelController = friendGDI.levelController;
				friendView.dwellingController = friendGDI.dwellingController;				
				friendView.addEventListener("worldAdded", onFriendViewWorldAdded);
			}
			private function onFriendViewWorldAdded(evt:DynamicEvent):void
			{
				friendView.removeEventListener("worldAdded", onFriendViewWorldAdded);
				friendView.startFriendMirror();		
			}
			public function showFriendVenuePostLoad(friendGDI:GameDataInterface):void
			{
				currentState = "friendView";
				friendView.myUser = friendGDI.user;
				friendView.userController = friendGDI.userController;
				friendView.structureController = friendGDI.structureController;
				friendView.layerableController = friendGDI.layerableController;
				friendView.creatureController = friendGDI.creatureController;	
				friendView.levelController = friendGDI.levelController;				
				friendView.dwellingController = friendGDI.dwellingController;				
				friendView.startFriendMirror();				
			}
			public function instancesLoadedForGameUser():void
			{

			}
			public function instancesLoadedForFriend():void
			{
				
			}
			private function onUserObjectLoaded(evt:ServerDataEvent):void
			{
				topBarView.onUserObjectLoaded();
				bottomBarView.onUserObjectLoaded();				
			}
			private function onGameContentLoaded(evt:ServerDataEvent):void
			{

			}
			private function onUserContentLoaded(evt:ServerDataEvent):void
			{
				setGameContent();
				gdi.checkIfLoadingAndInstantiationComplete();				
			}
			public function onCreatureToCustomize(creature:Creature):void
			{
				currentState = 'twoDimensional';
				twoDimensionalView.removeAllChildren();
				
				var creatureCustomizer:CreatureCustomizer = gdi.creatureController.generateCreatureCustomizer(creature);
				creatureCustomizer.x = 0;
				creatureCustomizer.y = 500;
				twoDimensionalView.addChild(creatureCustomizer);
			}
			private function onWorldThingerPurchased(evt:UserEvent):void
			{
				gdi.userController.incrementCredits(evt.creditsToAdd);
			}
			private function onUpdateComplete(evt:ServerDataEvent):void
			{
				var instance:Object = evt.params;
				
				if (evt.model == "owned_dwelling")
				{
					checkForUpdatedVenue(instance as OwnedDwelling, evt.method);				
				}
				if (evt.model == "owned_structure")
				{
					updateOwnedStructurePostServer(instance as OwnedStructure, evt.method);
				}
				if (evt.model == "owned_layerable")
				{
					updateOwnedLayerablePostServer(instance as OwnedLayerable, evt.method);
				}
				if (evt.model == "user")
				{
					if (evt.method == "get_friend_basics")
					{	
						friendManager.processBasicFriendData(instance as User, evt.method);
					}
					else
					{
						gdi.user.updateUserOnServerResponse(instance as User, evt.method);
						gdi.levelController.setLevelOnUser(gdi.user);
					}
				}
			}
			private function updateOwnedStructurePostServer(os:OwnedStructure, method:String):void
			{
				gdi.structureController.updateOwnedStructureOnServerResponse(os, method, worldView);
				worldView.updateRenderedStructures(os, method);
				stageView.updateRenderedStructures(os, method, stageView.myStage);
				
				if (editView)
				{
					editView.updateRenderedStructures(os, method);					
				}				
			}
			private function updateOwnedLayerablePostServer(ol:OwnedLayerable, method:String):void
			{
				var creature:Creature = gdi.creatureController.getCreatureById(ol.creature_id);
				if (creature.type == "BandMember")
				{
					worldView.bandBoss.bandMemberManager.updateRenderedBandMembers(ol, creature, method);
				}
				gdi.layerableController.updateOwnedLayerableOnServerResponse(ol, method);
			}
			private function checkForUpdatedVenue(instance:OwnedDwelling, method:String):void
			{
				if (instance.id == worldView.venueManager.venue.id)
				{
					worldView.venueManager.onVenueUpdated(method, instance);
				}
			}
			private function onThingerPurchased(evt:StoreEvent):void
			{
				if (evt.thinger.structure)
				{
					var os:OwnedStructure = new ImposterOwnedStructure(evt.thinger);
					editView.pausedForStateCompletion = os;
					switchToEditView();
					if (editView.editMode)
					{
						var asset:ActiveAsset = editView.editMode.createPurchaseCopy(os);
						editView.pausedForStateCompletion = null;					
						editView.editMode.startMoveWithoutEditOptions(asset);
					}
				}
			}
			public function switchToEditView():void
			{
				currentState = "editView";
				editView.addEventListener("editViewCreated", onEditViewCreated);
			}
			private function onEditViewCreated(evt:DynamicEvent):void
			{
				if (editView.pausedForStateCompletion)
				{
					var asset:ActiveAsset = editView.editMode.createPurchaseCopy(editView.pausedForStateCompletion);
					editView.pausedForStateCompletion = null;					
					editView.editMode.startMoveWithoutEditOptions(asset);					
				}
			}
			
		]]>
	</mx:Script>
</mx:Application>